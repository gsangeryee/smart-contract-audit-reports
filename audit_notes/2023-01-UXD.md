# 2023-01-UXD
---
- Category: #liquid_staking #services #Derivatives #yield_aggregator #cross-chain #Perpetuals
- Note Create 2025-01-24
- Platform: Sherlock
- Report Url: [2023-01-UXD](https://app.sherlock.xyz/audits/contests/33)
---
# High Risk Findings (xx)

---
## [H-01] `PerpDespository#reblance` and `rebalanceLite` can be called to drain funds from anyone who has approved `PerpDepository`
----
- **Tags**: #business_logic #allowance
- Number of finders: 15
- Difficulty: Easy
---
### Summary

`PerpDespository.reblance` and `rebalanceLite` allows anyone to specify the account that pays the quote token. These functions allow a malicious user to abuse any allowance provided to `PerpDirectory.rebalance` is the worst of the two because the malicious user could sandwich attack the rebalance to steal all the funds and force the unsuspecting user to pay the `shortfall`.
### Vulnerability Detail

```solidity
function rebalance(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    int8 polarity,
    address account // @audit user specified payer
) external nonReentrant returns (uint256, uint256) {
    if (polarity == -1) {
        return
            _rebalanceNegativePnlWithSwap(
                amount,
                amountOutMinimum,
                sqrtPriceLimitX96,
                swapPoolFee,
                account // @audit user address passed directly
            );
    } else if (polarity == 1) {
        // disable rebalancing positive PnL
        revert PositivePnlRebalanceDisabled(msg.sender);
        // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
    } else {
        revert InvalidRebalance(polarity);
    }
}
```

`rebalance` is an unpermissioned function that allows anyone to call and rebalance the PNL of the depository. It allows the caller to specify the an account that passes directly through to `_rebalanceNegativePnlWithSwap`

```solidity
function _rebalanceNegativePnlWithSwap(
    uint256 amount,
    uint256 amountOutMinimum,
    uint160 sqrtPriceLimitX96,
    uint24 swapPoolFee,
    address account
) private returns (uint256, uint256) {
    ...
    // @audit this uses user supplied swap parameters which can be malicious
    SwapParams memory params = SwapParams({
        tokenIn: assetToken,
        tokenOut: quoteToken,
        amountIn: baseAmount,
        amountOutMinimum: amountOutMinimum,
        sqrtPriceLimitX96: sqrtPriceLimitX96,
        poolFee: swapPoolFee
    });
    uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
    int256 shortFall = int256(
        quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
    ) - int256(quoteAmountOut);
    if (shortFall > 0) {
        // @audit shortfall is taken from account specified by user
        IERC20(quoteToken).transferFrom(
            account,
            address(this),
            uint256(shortFall)
        );
    } else if (shortFall < 0) {
        ...
    }
    vault.deposit(quoteToken, quoteAmount);

    emit Rebalanced(baseAmount, quoteAmount, shortFall);
    return (baseAmount, quoteAmount);
}
```

`_rebalanceNegativePnlWithSwap` uses both user specified swap parameters and takes the shortfall from the account specified by the user. This is where the function can be abused to steal funds from any user that sets an allowance for this contract. A malicious user can sandwich attack the swap and specify malicious swap parameters to allow them to steal the entire rebalance. This creates a large shortfall which will be taken from the account that they specify, effectively stealing the funds from the user.

Example:  
Any account that gives the depository allowance can be stolen from. Imagine the following scenario. The multisig is going to rebalance the contract for 15000 USDC worth of ETH and based on current market conditions they are estimating that there will be a 1000 USDC shortfall because of the difference between the perpetual and spot prices (divergences between spot and perpetual price are common in trending markets). They first approve the depository for 1000 USDC. A malicious user sees this approval and immediately submits a transaction of their own. They request to rebalance only 1000 USDC worth of ETH and sandwich attack the swap to steal the rebalance. They specify the multisig as `account` and force it to pay the 1000 USDC shortfall and burn their entire allowance, stealing the USDC.

### Impact

Anyone that gives the depository allowance can easily have their entire allowance stolen

### Recommendation

`PerpDespository#reblance` and `rebalanceLite` should use `msg.sender` instead of account:

```solidity
     function rebalance(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        int8 polarity,
-       address account
    ) external nonReentrant returns (uint256, uint256) {
        if (polarity == -1) {
            return
                _rebalanceNegativePnlWithSwap(
                    amount,
                    amountOutMinimum,
                    sqrtPriceLimitX96,
                    swapPoolFee,
-                   account 
+                   msg.sender
                );
        } else if (polarity == 1) {
            // disable rebalancing positive PnL
            revert PositivePnlRebalanceDisabled(msg.sender);
            // return _rebalancePositivePnlWithSwap(amount, amountOutMinimum, sqrtPriceLimitX96, swapPoolFee, account);
        } else {
            revert InvalidRebalance(polarity);
        }
    }
```
### Discussion

### Notes

#### Notes 
1. **The victim pays the shortfall**:  
    The attacker manipulates swap parameters (e.g., setting a low `amountOutMinimum`) to create a large shortfall. The contract forcibly transfers this shortfall amount from the **victim’s address** (specified as `account`) to itself using `transferFrom`.
    
2. **The victim’s allowance is drained**:  
    The victim must have previously approved the `PerpDepository` contract to spend their tokens (e.g., USDC). The shortfall amount is deducted directly from the victim’s allowance (up to the approved limit). If the shortfall exceeds the allowance, the transaction will fail, but attackers typically maximize the shortfall to drain the entire allowance.
#### Example Flow:

- **Victim’s allowance**: 1,000 USDC (approved to `PerpDepository`).
    
- **Attacker’s action**: Calls `rebalance`, sets `account = victim`, manipulates swap to create a 1,000 USDC shortfall.
    
- **Result**:
    
    - The contract uses `transferFrom` to pull 1,000 USDC from the victim.
        
    - The victim’s allowance is reduced to **0**, and their funds are stolen.
#### Impressions

The shortfall is a **deficit created during rebalancing**, and the vulnerability allows attackers to **force unsuspecting users to pay for it**, draining their approved funds. Fixing this requires ensuring only the transaction caller (`msg.sender`) can be charged for shortfalls.

### Tools
### Refine

- [[1-Business_Logic]]
- [[18-Allowance]]
- [[terms#Shortfall|Shortfall]]

---
## [H-03] RageTrade senior vault USDC deposits are subject to utilization caps which can lock deposits for long periods of time leading to UXD instability
----
- **Tags**: #business_logic #min/max_cap_validation
- Number of finders: 4
- Difficulty: Medium
---
### Summary

RageTrade senior vault requires that it maintains deposits above and beyond the current amount loaned to the junior vault. Currently this is set at 90%, that is the vault must maintain at least 10% more deposits than loans. Currently the junior vault is in high demand and very little can be withdrawn from the senior vault. A situation like this is far from ideal because in the even that there is a strong depeg of UXD a large portion of the collateral could be locked in the vault unable to be withdrawn.

### Vulnerability Detail

[DnGmxSeniorVault.sol](https://arbiscan.io/address/0x66aca71a2e62022f9f23a50ab737ded372ad00cf#code#F31#L288)

```solidity
function beforeWithdraw(
    uint256 assets,
    uint256,
    address
) internal override {
    /// @dev withdrawal will fail if the utilization goes above maxUtilization value due to a withdrawal
    // totalUsdcBorrowed will reduce when borrower (junior vault) repays
    if (totalUsdcBorrowed() > ((totalAssets() - assets) * maxUtilizationBps) / MAX_BPS)
        revert MaxUtilizationBreached();

    // take out required assets from aave lending pool
    pool.withdraw(address(asset), assets, address(this));
}
```

`DnGmxSeniorVault.sol#beforeWithdraw` is called before each withdraw and will revert if the withdraw lowers the utilization of the vault below a certain threshold. This is problematic in the event that large deposits are required to maintain the stability of UXD.

### Impact

UXD may become destabilized in the event that the senior vault has high utilization and the collateral is inaccessible

### Recommendation

I recommend three safeguards against this:

1. Monitor the current utilization of the senior vault and limit deposits if utilization is close to locking positions
    
2. Maintain a portion of the USDC deposits outside the vault (i.e. 10%) to avoid sudden potential liquidity crunches
    
3. Create functions to balance the proportions of USDC in and out of the vault to withdraw USDC from the vault in the event that utilization threatens to lock collateral
### Discussion

**WarTech9**

Possible usecase for insurance fund.

**acamill**

This is the main downside of using not fully liquid strategies for the ALM model, upside being higher yield.  
We can mitigate this issue with buffers but that's always an issue, and adding buffers with either protocol funds or insurance fund is equivalent to using lower yield strategies, as such not an ideal solution either. (and it add complexity)

My personal opinion is to keep the cap on the illiquid strategy to be low enough relative to the total circulating UXD, that way keeping the high yield but reducing the liquidity crunch issue.  
That's what we are currently doing on Solana, working on smarter rebalancing and better risk management to keep these cap relevant.
### Notes

#### Notes 
The senior vault enforces a **utilization cap** (e.g., 90%) to ensure it always retains a liquidity buffer (e.g., 10% of deposits). If utilization exceeds this cap, withdrawals are blocked to prevent insolvency.

- **Example**:
    
    - Senior vault total deposits = 100 USDC.
        
    - Junior vault has borrowed 90 USDC (90% utilization).
        
    - If a user tries to withdraw 5 USDC from the senior vault, total deposits would drop to 95 USDC.
        
#### Impressions

*The core issue revolves around the **utilization cap** (max cap) implemented in the RageTrade senior vault, which is designed to ensure solvency but inadvertently introduces a liquidity risk by potentially blocking withdrawals during periods of **high demand**.*

**Safety vs. Liquidity Trade-off**
- **Safety Measure**: The cap protects against over-borrowing and insolvency.
    
- **Liquidity Risk**: The same cap restricts access to funds precisely when needed (e.g., crises), creating a **Catch-22**:
    
    - High utilization maximizes yield (via lending) but minimizes liquidity.
        
    - The buffer (10%) is too small to handle sudden withdrawals during stress.
### Tools
### Refine

- [[1-Business_Logic]]
- [[19-MinOrMax_Cap_Validation]]

---
## [H-05] USDC deposited to `PerpDepository.sol` are irretrievable and effectively causes UDX to become `undercollateralized`
----
- **Tags**: #overflow/underflow #business_logic 
- Number of finders: 2
- Difficulty: Medium
---
### Summary

PerpDepository rebalances negative PNL into USDC holdings. This preserves the delta neutrality of the system by exchanging base to quote. This is problematic though as once it is in the vault as USDC it can never be withdrawn. The effect is that the delta neutral position can never be liquidated but the USDC is inaccessible so UDX is effectively undercollateralized.

### Vulnerability Detail

`_processQuoteMint`, `_rebalanceNegativePnlWithSwap` and `_rebalanceNegativePnlLite` all add USDC collateral to the system. There were originally two ways in which USDC could be removed from the system. The first was positive PNL rebalancing, which has now been deactivated. The second is for the owner to remove the USDC via `withdrawInsurance`.

```solidity
function withdrawInsurance(uint256 amount, address to)
    external
    nonReentrant
    onlyOwner
{
    if (amount == 0) {
        revert ZeroAmount();
    }

    insuranceDeposited -= amount;

    vault.withdraw(insuranceToken(), amount);
    IERC20(insuranceToken()).transfer(to, amount);

    emit InsuranceWithdrawn(msg.sender, to, amount);
}
```

The issue is that `withdrawInsurance` cannot actually redeem any USDC. Since insuranceDeposited is a uint256 and is decremented by the withdraw, it is impossible for more USDC to be withdrawn then was originally deposited.

The result is that there is no way for the USDC to ever be redeemed and therefore over time will lead to the system becoming undercollateralized due to its inaccessibility.

### Impact

UDX will become undercollateralized and the ecosystem will spiral out of control

### Code Snippet

[integrations/perp/PerpDepository.sol#L478-L528](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L478-L528)

```solidity
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96,
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
        int256 shortFall = int256(
            quoteAmount.fromDecimalToDecimal(18, ERC20(quoteToken).decimals())
        ) - int256(quoteAmountOut);
        if (shortFall > 0) {
            IERC20(quoteToken).transferFrom(
                account,
                address(this),
                uint256(shortFall)
            );
        } else if (shortFall < 0) {
            // we got excess tokens in the spot swap. Send them to the account paying for rebalance
            IERC20(quoteToken).transfer(
                account,
                _abs(shortFall)
            );
        }
        vault.deposit(quoteToken, quoteAmount);


        emit Rebalanced(baseAmount, quoteAmount, shortFall);
        return (baseAmount, quoteAmount);
    }
```

[integrations/perp/PerpDepository.sol#L615-L644](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L615-L644)

```solidity
    function _rebalanceNegativePnlLite(
        uint256 amount,
        uint160 sqrtPriceLimitX96,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );


        _checkNegativePnl(normalizedAmount);
        IERC20(quoteToken).transferFrom(account, address(this), amount);
        IERC20(quoteToken).approve(address(vault), amount);
        vault.deposit(quoteToken, amount);


        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        IERC20(assetToken).transfer(account, baseAmount);


        emit Rebalanced(baseAmount, quoteAmount, 0);


        return (baseAmount, quoteAmount);
    }
```

[integrations/perp/PerpDepository.sol#L385-L397](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L385-L397)

```solidity
    function _processQuoteMint(uint256 quoteAmount) private returns (uint256) {
        uint256 normalizedAmount = quoteAmount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        quoteMinted += int256(normalizedAmount);
        redeemableUnderManagement += normalizedAmount;
        _checkSoftCap();
        IERC20(quoteToken).approve(address(vault), quoteAmount);
        vault.deposit(quoteToken, quoteAmount);
        return normalizedAmount;
    }
```
### Recommendation

Allow all USDC now deposited into the insurance fund to be redeemed 1:1

```solidity
function _rebalanceNegativePnlWithSwap(...) {
    ...
    vault.deposit(quoteToken, quoteAmount);
    insuranceDeposited += quoteAmount; // Track rebalance deposits
}
```
### Notes

#### Notes 
#### **1. Core Mechanism: UDX Stability & Rebalancing**

- **UDX** is a stablecoin intended to be **delta-neutral** (immune to market price swings) and **collateralized 1:1 by assets (like USDC)**.
    
- **PerpDepository** handles rebalancing to maintain this delta neutrality. When the system incurs a **negative PNL** (loss), it must adjust by converting some assets (like ETH) into USDC to offset losses and keep UDX collateralized.
#### **2. How USDC Gets Trapped**

 **Deposit Paths:**

- **`_processQuoteMint`**: Mints UDX against deposited USDC (normal operation).
    
- **`_rebalanceNegativePnlWithSwap`**: Converts asset (e.g., ETH) to USDC during rebalancing and deposits USDC into the vault.
    
- **`_rebalanceNegativePnlLite`**: Directly pulls USDC from users during rebalancing and deposits it into the vault.

**Problem:**

- **USDC from rebalancing** is deposited into the vault **but not tracked** in `insuranceDeposited`, a variable meant to track withdrawable USDC.
    
- **`withdrawInsurance`** is the only way to retrieve USDC, but it decrements `insuranceDeposited`. Since rebalance-deposited USDC isn’t recorded here, it’s **impossible to withdraw them**.
#### Impressions

*Untracked USDC in Rebalancing*

```solidity
// Example: _rebalanceNegativePnlWithSwap deposits USDC but skips insuranceDeposited
vault.deposit(quoteToken, quoteAmount); // USDC added to vault
// Missing: insuranceDeposited += quoteAmount;
```

The protocol’s stability hinges on **accurate accounting of all collateral**. By updating `insuranceDeposited` during rebalancing, the system ensures transparency, accessibility, and solvency, safeguarding UDX’s peg and user trust.
### Tools
### Refine

- [[1-Business_Logic]]
- [[9-Overflow_Underflow]]

---

# Medium Risk Findings (xx)

---
## [M-02] `PerpDepository._rebalanceNegativePnlWithSwap()` shouldn't use a `sqrtPriceLimitX96` twice.
----
- **Tags**: #business_logic 
- Number of finders: 1
- Difficulty: Medium
---
### Summary

`PerpDepository._rebalanceNegativePnlWithSwap()` shouldn't use a `sqrtPriceLimitX96` twice.

### Vulnerability Detail

Currently, `_rebalanceNegativePnlWithSwap()` uses a `sqrtPriceLimitX96` param twice for placing a perp order and swapping.

```solidity
    function _rebalanceNegativePnlWithSwap(
        uint256 amount,
        uint256 amountOutMinimum,
        uint160 sqrtPriceLimitX96,
        uint24 swapPoolFee,
        address account
    ) private returns (uint256, uint256) {
        uint256 normalizedAmount = amount.fromDecimalToDecimal(
            ERC20(quoteToken).decimals(),
            18
        );
        _checkNegativePnl(normalizedAmount);
        bool isShort = false;
        bool amountIsInput = true;
        (uint256 baseAmount, uint256 quoteAmount) = _placePerpOrder(
            normalizedAmount,
            isShort,
            amountIsInput,
            sqrtPriceLimitX96
        );
        vault.withdraw(assetToken, baseAmount);
        SwapParams memory params = SwapParams({
            tokenIn: assetToken,
            tokenOut: quoteToken,
            amountIn: baseAmount,
            amountOutMinimum: amountOutMinimum,
            sqrtPriceLimitX96: sqrtPriceLimitX96, //@audit 
            poolFee: swapPoolFee
        });
        uint256 quoteAmountOut = spotSwapper.swapExactInput(params);
```

In `_placePerpOrder()`, it uses the uniswap pool inside the perp protocol and uses a `spotSwapper` for the second swap which is for the uniswap as well.

But as we can see [here](https://docs.uniswap.org/concepts/protocol/fees#pool-fees-tiers), Uniswap V3 introduces multiple pools for each token pair and 2 pools might be different and I think it's not good to use the same `sqrtPriceLimitX96` for different pools.

Also, I think it's not mandatory to check a `sqrtPriceLimitX96` as it checks `amountOutMinimum` already. (It checks `amountOutMinimum` only in `_openLong()` and `_openShort()`.)

## Impact

`PerpDepository._rebalanceNegativePnlWithSwap()` might revert when it should work as it uses the same `sqrtPriceLimitX96` for different pools.

## Recommendation

I think we can use the `sqrtPriceLimitX96` param for one pool only and it would be enough as there is an `amountOutMinimum` condition.
### Notes & Impressions

#### Notes 
 **1. What is `sqrtPriceLimitX96`?**

- A parameter in Uniswap V3 that defines the **maximum/minimum price** a swap will accept, protecting against slippage.
    
- **Pool-Specific**: Each Uniswap V3 pool (even for the same token pair with different fees) has its own price curve. Thus, the same `sqrtPriceLimitX96` value represents different absolute prices in different pools.
 **2. Issue in `_rebalanceNegativePnlWithSwap`**

- **Two Swaps, One Parameter**:
    
    - **First Swap**: `_placePerpOrder` interacts with a Perp protocol-internal Uniswap pool.
        
    - **Second Swap**: `spotSwapper.swapExactInput` uses a different external Uniswap pool (potentially a different fee tier).
        
    - Both swaps use the **same `sqrtPriceLimitX96`** value passed to the function.
        
- **Problem**:
    
    - If the two pools have **different current prices**, the same `sqrtPriceLimitX96` may:
        
        - **Over-restrict** one swap (causing unnecessary reverts).
            
        - **Under-protect** the other (allowing excessive slippage).
            
    - Example:
        
        - Pool A (Perp’s pool) has a price of 1000 USDC/ETH.
            
        - Pool B (external pool) has a price of 990 USDC/ETH.
            
        - Using `sqrtPriceLimitX96` derived for Pool A in Pool B could force the second swap to revert, even if the slippage is acceptable.
#### Impressions

Different pool have different `sqrtPriceLimitX96` (**maximum/minimum price** a swap will accept)
in Uniswap V3
### Tools
### Refine

- [[1-Business_Logic]]

---
## [M-06] Inaccurate Perp debt calculation
----
- **Tags**: #wrong_math #business_logic #PCPvsSCP 
- Number of finders: 4
- Difficulty: Medium
---
## Summary

The anticipated Perp account debt value calculation via `PerpDepository.getDebtValue` is inaccurate and does not incorporate the (not yet settled) owed realized PnL `owedRealizedPnl`.

## Vulnerability Detail

The `PerpDepository.getDebtValue` function calculates the account debt value by subtracting the pending funding payments and fees from the quote token balance and unrealized PnL. However, the owed realized PnL (`owedRealizedPnl`) is not considered in the calculation. The owed realized PnL is the realized PnL owed to the account but has **not yet been settled**.

Perp provides the `Vault.getSettlementTokenValue()` function to calculate the settlement token value of an account and uses it to determine the accounts' debt (if < 0, [see docs](https://docs.perp.com/docs/contracts/Vault/#getsettlementtokenvalue)). For example, it is used to determine if an account is liquidable - see [Vault.isLiquidatable#L434](https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L434)

Perps' specs define the value of an account as ([see here for reference](https://support.perp.com/hc/en-us/articles/5331515119001)):

$$  
\begin{aligned} accountValue &= \underbrace{collateral + owedRealizedPnl + pendingFundingPayment + pendingFee}_{totalCollateralValue} + \underbrace{\sum_{market}{unrealizedPnl_{market}}}_{totalUnrealizedPnl} \end{aligned}  
$$

## Impact

The Perp account debt calculation is inaccurate and deviates from the calculation by the Perp protocol itself. Even though the `PerpDepository.getDebtValue` function is `external`, it could lead to issues when querying from another contract or off-chain to use as decision criteria or manifest as a serious issue when used in an upgraded version of the contract.

## Code Snippet

[integrations/perp/PerpDepository.sol#L773](https://github.com/sherlock-audit/2023-01-uxd/blob/main/contracts/integrations/perp/PerpDepository.sol#L773)

```solidity
/// @notice Get the quote token balance of this user
/// @dev THe total debt is computed as:
///     quote token balance + unrealized PnL - Pending fee - pending funding payments
/// @param account The account to return the debt for
/// @return debt The account debt, or zero if no debt.
function getDebtValue(address account) external view returns (uint256) {
    IAccountBalance perpAccountBalance = IAccountBalance(
        clearingHouse.getAccountBalance()
    );
    IExchange perpExchange = IExchange(clearingHouse.getExchange());
    int256 accountQuoteTokenBalance = vault.getBalance(account);
    if (accountQuoteTokenBalance < 0) {
        revert InvalidQuoteTokenBalance(accountQuoteTokenBalance);
    }
    int256 fundingPayment = perpExchange.getAllPendingFundingPayment(
        account
    );
    uint256 quoteTokenBalance = uint256(accountQuoteTokenBalance)
        .fromDecimalToDecimal(ERC20(quoteToken).decimals(), 18);
    (
        , // @audit-info `owedRealizedPnl` is omitted here and missing in the calculation below
        int256 perpUnrealizedPnl,
        uint256 perpPendingFee
    ) = perpAccountBalance.getPnlAndPendingFee(account);
    int256 debt = int256(quoteTokenBalance) +
        perpUnrealizedPnl -
        int256(perpPendingFee) -
        fundingPayment;
    return (debt > 0) ? 0 : _abs(debt);
}
```

[Vault._getSettlementTokenValue](https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L880-L884)

`Vault._getSettlementTokenValue` is called internally by Perp's public `Vault.getSettlementTokenValue()` function.

```solidity
function _getSettlementTokenValue(address trader) internal view returns (int256 settlementTokenValueX10_18) {
    (int256 settlementBalanceX10_18, int256 unrealizedPnlX10_18) =
        _getSettlementTokenBalanceAndUnrealizedPnl(trader);
    return settlementBalanceX10_18.add(unrealizedPnlX10_18);
}
```

[Vault.\_getSettlementTokenBalanceAndUnrealizedPnl](https://github.com/perpetual-protocol/perp-curie-contract/blob/27ea8e2c4be37d1dd58c1eed3b3cc269d398a091/contracts/Vault.sol#L852-L877)

```solidity
/// @notice Get the specified trader's settlement token balance, including pending fee, funding payment,
///         owed realized PnL, but without unrealized PnL)
/// @dev Note the difference between the return argument`settlementTokenBalanceX10_18` and
///      the return value of `getSettlementTokenValue()`.
///      The first one is settlement token balance with pending fee, funding payment, owed realized PnL;
///      The second one is the first one plus unrealized PnL.
/// @return settlementTokenBalanceX10_18 Settlement amount in 18 decimals
/// @return unrealizedPnlX10_18 Unrealized PnL in 18 decimals
function _getSettlementTokenBalanceAndUnrealizedPnl(address trader)
    internal
    view
    returns (int256 settlementTokenBalanceX10_18, int256 unrealizedPnlX10_18)
{
    int256 fundingPaymentX10_18 = IExchange(_exchange).getAllPendingFundingPayment(trader);

    int256 owedRealizedPnlX10_18;
    uint256 pendingFeeX10_18;
    (owedRealizedPnlX10_18, unrealizedPnlX10_18, pendingFeeX10_18) = IAccountBalance(_accountBalance)
        .getPnlAndPendingFee(trader);

    settlementTokenBalanceX10_18 = getBalance(trader).parseSettlementToken(_decimals).add(
        pendingFeeX10_18.toInt256().sub(fundingPaymentX10_18).add(owedRealizedPnlX10_18) // @audit-info owed realized PnL is added here
    );

    return (settlementTokenBalanceX10_18, unrealizedPnlX10_18);
}
```

## Recommendation

Consider using the `Vault.getSettlementTokenValue()` function to determine the accounts' debt ([see docs](https://docs.perp.com/docs/contracts/Vault/#getsettlementtokenvalue)).
### Notes & Impressions

#### Notes 
The `getDebtValue` function calculates the debt using the formula:
```
debt = quoteTokenBalance + unrealizedPnl - pendingFee - fundingPayment
```

but overlooked the Perp specification, which explicitly includes `owedRealizedPnl` in the collateral value:

```
totalCollateralValue = collateral + owedRealizedPnl + pendingFundingPayment + pendingFee
```

#### Impressions

I'm curious about how this issue occurred. It seems that one developer wrote the `Vault.getSettlementTokenValue()` function to calculate Perp's debt, but another developer (or the same one) wrote the `PerpDepository.getDebtValue` function without using `getSettlementTokenValue()`. Instead, they manually calculated the debt, which led to this error.

- *Double-check the complex calculation*
### Tools
### Refine

- [[1-Business_Logic]]
- [[3-Wrong_Math]]

---

---

## Audit Summary Notes
- {{summary_notes}}

## Tags
- Category: {{tags_category}}
- Priority:{{tags_priority}}