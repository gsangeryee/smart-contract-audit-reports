# 2023-03-Morpho
---
- Category: #CDP #Derivatives #Dexes #services #yield_aggregator #compound
- Note Create 2024-12-31
- Platform: Spearbit
- Report Url: [2023-03-Morpho](https://github.com/spearbit/portfolio/blob/master/pdfs/MorphoV1-Spearbit-Security-Review.pdf)
---
# High Risk Findings (xx)

---

{{Copy from High Risk Finding Template.md}}

---

# Medium Risk Findings (xx)

---
## [M-01] A market could be deprecated but still prevent liquidators to liquidate borrowers if `isLiquidateBorrowPaused` is `true`
----
- **Tags**: #Pause #business_logic #COMP
- Number of finders: 5
- Difficulty: Medium
---
### Description

Currently, when a market must be deprecated, Morpho checks that borrowing has been paused before applying the new value for the flag.

[aave-v2/MorphoGovernance.sol#L358-L366](https://github.com/morpho-org/morpho-optimizers/blob/4810ba9830d606430a9cb93936da69753556502e/src/aave-v2/MorphoGovernance.sol#L358-L366) and [compound/MorphoGovernance.sol#L368-L376](https://github.com/morpho-org/morpho-optimizers/blob/4810ba9830d606430a9cb93936da69753556502e/src/compound/MorphoGovernance.sol#L368-L376)
```solidity
    function setIsDeprecated(address _poolToken, bool _isDeprecated)
        external
        onlyOwner
        isMarketCreated(_poolToken)
    {
        if (!marketPauseStatus[_poolToken].isBorrowPaused) revert BorrowNotPaused();
        marketPauseStatus[_poolToken].isDeprecated = _isDeprecated;
        emit IsDeprecatedSet(_poolToken, _isDeprecated);
    }
```

The same check should be done in `isLiquidateBorrowPaused`, allowing the deprecation of a market only if `isLiquidateBorrowPaused == false` otherwise liquidators would not be able to liquidate borrowers on a deprecated market.
### Recommended Mitigation

Prevent the deprecation of a market if the `isLiquidateBorrowPaused` flag is set to true. 

Consider also checking the `isDeprecated` flag in the `setIsLiquidateBorrowPaused` to prevent pausing the liquidation if the market is deprecated. If Morpho implements the specific behavior should also be aware of the issue described in "*`setIsPausedForAllMarkets` bypass the check done in `setIsBorrowPaused` and allow resuming borrow on a deprecated market*".

Example:
```solidity
function setIsDeprecated(address _poolToken, bool _isDeprecated)
    external
    onlyOwner
    isMarketCreated(_poolToken)
{
    if (!marketPauseStatus[_poolToken].isBorrowPaused) revert BorrowNotPaused();
    // Add new check
    if (marketPauseStatus[_poolToken].isLiquidateBorrowPaused) revert LiquidationsPaused();
    marketPauseStatus[_poolToken].isDeprecated = _isDeprecated;
    emit IsDeprecatedSet(_poolToken, _isDeprecated);
}
```

### Discussion

Morpho: We acknowledge this issue. The reason behind this is the following: given what @MathisGD said, if we want to be consistent we should prevent pausing the liquidation borrow on a deprecated asset. However, there might be an issue (we don't know) with the liquidation borrow and the operator would not be able to pause it. For this reason, we prefer to leave things as it is. 

Spearbit: Acknowledged.

### Notes & Impressions

#### Notes 
First, let's understand what these mechanisms do:

1. Market Deprecation: When a market needs to be shut down or retired, it can be "deprecated." This is typically done when there are issues with the market or it needs to be phased out.
2. Liquidation Pausing: The protocol has the ability to pause liquidations through the `isLiquidateBorrowPaused` flag. This is usually a temporary measure for maintenance or emergency situations.

The issue arises because there's no similar check for the liquidation status. This means a market could be deprecated while liquidations are paused (`isLiquidateBorrowPaused = true`). This creates a dangerous situation where:

1. The market is deprecated (meaning it should be wound down)
2. There might be borrowers who need to be liquidated to maintain system health
3. But liquidators cannot perform liquidations because they're paused
#### Impressions

**Key Points**
- Market Status (Deprecated/Closed)
    - Interaction restrictions
    - Order of operations during deprecation
    - Proper cleanup mechanisms
- Liquidation Status
    - Pause/unpause conditions
    - Emergency controls
    - Impact on protocol solvency
- Additional Critical Areas:
    - State transitions between market statuses
    - Oracle price feed reliability
    - Collateral value calculations
    - Fee mechanisms during market changes
    - Access control for status changes
    - Event emissions for status changes
### Tools
- [[Market-related]]
### Refine
- [[1-Business_Logic]]

---
## [M-04] P2P borrower's rate can be reduced
----
- **Tags**: #business_logic 
- Number of finders: 5
- Difficulty: Hard
---
### Original Context
(this area of the code has changed significantly since the initial audit, so maintaining the link to the original code base)

[aave/MarketsManagerForAave.sol#L436-L463](https://github.com/morpho-org/morpho-optimizers/blob/a861d50597ae047640727fd63afc3c3a9b0e1cbc/contracts/aave/MarketsManagerForAave.sol#L436-L463)
```solidity
    function _computeNewP2PExchangeRate(
        uint256 _p2pDelta,
        uint256 _p2pAmount,
        uint256 _p2pRate,
        uint256 _p2pSPY,
        uint256 _poolIndex,
        uint256 _lastPoolIndex,
        uint256 _timeDifference
    ) internal pure returns (uint256) {
        if (_p2pDelta == 0)
            return _p2pRate.rayMul(_computeCompoundedInterest(_p2pSPY, _timeDifference));
        else {
            uint256 shareOfTheDelta = _p2pDelta //@audit-issue
            .wadToRay()
            .rayMul(_p2pRate)
            .rayDiv(_poolIndex)
            .rayDiv(_p2pAmount.wadToRay());


            return
                _p2pRate.rayMul(
                    (
                        _computeCompoundedInterest(_p2pSPY, _timeDifference).rayMul(
                            WadRayMath.ray() - shareOfTheDelta
                        )
                    ) + (shareOfTheDelta.rayMul(_poolIndex).rayDiv(_lastPoolIndex))
                );
        }
    }
```
### Context
[aave-v2/InterestRatesManager.sol#L182](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/aave-v2/InterestRatesManager.sol#L182)
```
            newP2PBorrowIndex = _params.lastP2PBorrowIndex.rayMul(
```

[compound/InterestRatesManager.sol#L164](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/InterestRatesManager.sol#L164)
```solidity
            newP2PBorrowIndex = _params.lastP2PBorrowIndex.mul(
```
### Description: 
Users on the pool currently earn a much worse rate than users with P2P credit lines. There's a queue for being connected P2P. As this queue could not be fully processed in a single transaction the protocol introduces the concept of a max iteration count and a borrower/supplier "delta" (c.f. yellow paper). This delta leads to a worse rate for existing P2P users. An attacker can force a delta to be introduced, leading to worse rates than before.
### Example: 
Imagine some borrowers are matched P2P (earning a low borrow rate), and many are still on the pool and therefore in the pool queue (earning a worse borrow rate from Aave). 
- An attacker supplies a huge amount, creating a P2P credit line for every borrower. (They can repeat this step several times if the max iterations limit is reached.)
- The attacker immediately withdraws the supplied amount again. The protocol now attempts to demote the borrowers and reconnect them to the pool. But the algorithm performs a "hard withdraw" as the last step if it reaches the max iteration limit, creating a borrower delta. These are funds borrowed from the pool (at a higher borrowing rate) that are still wrongly recorded to be in a P2P position for some borrowers. This increase in borrowing rate is socialized equally among all P2P borrowers. (reflected in an updated p2pBorrowRate as the shareOfDelta increased.)
- The initial P2P borrowers earn a worse rate than before. If the borrower delta is large, it's close to the on-pool rate. 
- If an attacker-controlled borrower account was newly matched P2P and not properly reconnected to the pool (in the "demote borrowers" step of the algorithm), they will earn a better P2P rate than the on-pool rate they earned before. 

### Recommendation

Consider mitigations for single-transaction flash supply & withdraw attacks. 
### Discussion

Morpho: We acknowledge the risk and won't be fixing this issue, as we might want to refactor the entire queue system at some point. 
Spearbit: Acknowledged.
### Notes & Impressions

#### Notes 
Let me explain the concept of "delta" in this context and how it affects borrower rates. I'll use an analogy to make it clearer.

Imagine a lending marketplace with two floors:
- The ground floor is the "pool" (like Aave), where borrowing rates are higher
- The top floor is the "P2P floor", where rates are lower because borrowers are matched directly with lenders

The "delta" represents a mismatch between where borrowers are recorded to be and where they actually are. It's like some borrowers have their name tag on the P2P floor, but they're actually borrowing from the ground floor pool.

Let's walk through an example:

1. Initially:
   - Alice and Bob are on the P2P floor, borrowing at 5% interest
   - Charlie and Dave are on the ground floor, borrowing at 10% interest

2. The attack happens:
   - The attacker supplies enough money to move Charlie and Dave to the P2P floor (they get matched)
   - But then immediately withdraws their money
   - Due to technical limitations (max iterations), Charlie and Dave can't be properly moved back down
   - Result: Charlie and Dave are still labeled as "P2P borrowers" but are actually borrowing from the pool

3. This creates a "delta":
   - The system thinks Charlie and Dave are P2P borrowers (5% rate)
   - But they're actually pool borrowers (10% rate)
   - This 5% difference in rates needs to be paid by someone

Now, here's where the `shareOfTheDelta` calculation comes in:
```solidity
uint256 shareOfTheDelta = _p2pDelta        // How much is borrowed from pool but marked as P2P
    .wadToRay()                            // Convert to the right decimal format
    .rayMul(_p2pRate)                      // Multiply by the P2P rate
    .rayDiv(_poolIndex)                    // Divide by the pool's current index
    .rayDiv(_p2pAmount.wadToRay());        // Divide by total P2P amount
```

This calculation determines what portion of that extra cost (the delta between 5% and 10%) gets distributed to all P2P borrowers. The larger the mismatch (more people incorrectly marked as P2P), the more extra cost there is to distribute.

The result? Alice and Bob, who were legitimately on the P2P floor, now have to pay more than their original 5% rate. If the delta is large enough (many people incorrectly marked as P2P), Alice and Bob might end up paying close to the 10% pool rate, losing the benefit of being P2P matched.

This is why it's a vulnerability: an attacker can create a large delta through their flash supply/withdraw attack, forcing legitimate P2P borrowers to pay higher rates than they should.

#### Impressions
- *the special rate calculations*
- *Issues caused by the special mechanism*

### Tools
### Refine
- [[1-Business_Logic]]

---
## [M-3] User `withdrawals` can fail if Morpho position is close to liquidation
----
- **Tags**: #Denial_of_Service #business_logic 
- Number of finders: 4
- Difficulty: Medium
---
### Description

When trying to withdraw funds from Morpho as a P2P supplier the last step of the withdrawal algorithm borrows an amount from the pool ("hard withdraw"). If the Morpho position on Aave's debt / collateral value is higher than the market's max LTV ratio but lower than the market's liquidation threshold, the borrow will fail and the position can also not be liquidated. The withdrawals could fail.
### Proof of Concept

[ExitPositionsManager.sol#L468](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/aave-v2/ExitPositionsManager.sol#L468)
```solidity
    function _unsafeWithdrawLogic(
        address _poolToken,
        uint256 _amount,
        address _supplier,
        address _receiver,
        uint256 _maxGasForMatching
    ) internal {
......
        // Demote peer-to-peer borrowers.
        if (vars.remainingToWithdraw > 0) {
            uint256 unmatched = _unmatchBorrowers(
                _poolToken,
                vars.remainingToWithdraw,
                vars.remainingGasForMatching
            );

            // Increase the peer-to-peer borrow delta.
            if (unmatched < vars.remainingToWithdraw) {
                delta.p2pBorrowDelta += (vars.remainingToWithdraw - unmatched).rayDiv(
                    poolIndexes[_poolToken].poolBorrowIndex
                );
                emit P2PBorrowDeltaUpdated(_poolToken, delta.p2pBorrowDelta);
            }

            delta.p2pSupplyAmount -= Math.min(
                delta.p2pSupplyAmount,
                vars.remainingToWithdraw.rayDiv(vars.p2pSupplyIndex)
            );
            delta.p2pBorrowAmount -= Math.min(
                delta.p2pBorrowAmount,
                unmatched.rayDiv(p2pBorrowIndex[_poolToken])
            );
            emit P2PAmountsUpdated(_poolToken, delta.p2pSupplyAmount, delta.p2pBorrowAmount);

            _borrowFromPool(underlyingToken, vars.remainingToWithdraw); // Reverts on error.
        }
......
```

### Recommended Mitigation

This seems hard to solve in the current system as it relies on the "hard withdraws" to always ensure enough liquidity for P2P suppliers. Consider ways to mitigate the impact of this problem.

### Discussion

Morpho: Since Morpho will first launch on Compound (where there is only Collateral Factor), we will not focus now on this particular issue. 
Spearbit: Acknowledged.
### Notes & Impressions

#### Notes 
- Maximum LTV(Loan-to-Value ) ratio - The maximum amount you can borrow against your collateral
- Liquidation threshold: The point at which your position becomes eligible for liquidation

Imagine Aave has these parameters:
- Maximum LTV: 80%
- Liquidation threshold: 85%
This creates three possible zones for a lending position:
```
Safe Zone (LTV < 80%):
- Can borrow more
- Cannot be liquidated
- Normal operations

Danger Zone (80% < LTV < 85%): <- This is where our problem occurs
- Cannot borrow more
- Cannot be liquidated yet
- Position is stuck!

Liquidation Zone (LTV > 85%):
- Cannot borrow more
- Can be liquidated
- Position at risk
```

The scenario of withdrawals fail in the "Danger Zone"
- When a user requests a withdrawal from Morpho, and there isn't enough peer-to-peer liquidity, Morpho needs to borrow from Aave to fulfill the withdrawal (this is the "hard withdraw").
- But if Morpho's position is already in the Danger Zone (say at 82% LTV):
    - Morpho can't borrow more because it's above the max LTV (80%)
    - The position can't be liquidated because it hasn't reached the liquidation threshold (85%)
    - Therefore, there's no way to get the additional funds needed for the withdrawal
#### Impressions
- Pay attention to withdraw operations,
- Monitor the maximum LTV ratio,
- Be mindful of the liquidation threshold.

### Tools
### Refine
- [[1-Business_Logic]]
- [[5-Denial-Of-Service(DoS)]]

---
## [M-11] In Compound implementation, P2P indexes can be stale
----
- **Tags**: #business_logic 
- Number of finders: 5
- Difficulty: Hard
---
### Description

The current implementation of `MorphoUtils._isLiquidatable` loops through all of the tokens in which the user has supplied to/borrowed from. The scope of the function is to check whether the user can be liquidated or not by verifying that `debtValue > maxDebtValue`. 
Resolving "*Compound liquidity computation uses outdated cached borrowIndex*" implies that the Compound borrow index used is always up-to-date but the P2P issues associated with the token could still be out of date if the market has not been used recently, and the underlying Compound indexes (on which the P2P index is based) has changed a lot. 
As a consequence, all the functions that rely on `_isLiquidatable` (liquidate, withdraw, borrow) could return a wrong result if the majority of the user's balance is on the P2P balance (the problem is even more aggravated without resolving "*Compound liquidity computation uses outdated cached borrowIndex*".
Let's say, for example: 
- Alice supplies ETH in pool 
- Alice supplies BAT in P2P 
- Alice borrows some DAI 
At some point in time the ETH value goes down, but the interest rate of BAT goes up. If the P2P index of BAT had been correctly up-to-date, Alice would have been still solvent, but she gets liquidated by Bob who calls `liquidate`(alice, ETH, DAI) 
Even by fixing "*Compound liquidity computation uses outdated cached borrowIndex*" Alice would still be liquidated because her entire collateral is on P2P and not in the pool.

[compound/MorphoUtils.sol#L119-L156](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/MorphoUtils.sol#L119-L156)
```solidity
    function _isLiquidatable(
        address _user,
        address _poolToken,
        uint256 _withdrawnAmount,
        uint256 _borrowedAmount
    ) internal view returns (bool) {
        ICompoundOracle oracle = ICompoundOracle(comptroller.oracle());
        //@audit Get all markets the user has entered
        uint256 numberOfEnteredMarkets = enteredMarkets[_user].length;

		//@audit Initialize variables to track total debt and collateral values
        Types.AssetLiquidityData memory assetData;
        uint256 maxDebtValue;
        uint256 debtValue;
        uint256 i;

		//@audit Loop through all markets
        while (i < numberOfEnteredMarkets) {
            address poolTokenEntered = enteredMarkets[_user][i];

			//@audit Get liquidity data for each asset
            assetData = _getUserLiquidityDataForAsset(_user, poolTokenEntered, oracle);
            maxDebtValue += assetData.maxDebtValue;
            debtValue += assetData.debtValue;


            if (_poolToken == poolTokenEntered) {
                if (_borrowedAmount > 0)
                    debtValue += _borrowedAmount.mul(assetData.underlyingPrice);


                if (_withdrawnAmount > 0)
                    maxDebtValue -= _withdrawnAmount.mul(assetData.underlyingPrice).mul(
                        assetData.collateralFactor
                    );
            }


            unchecked {
                ++i;
            }
        }


        return debtValue > maxDebtValue;
    }
```

[compound/PositionsManager.sol#L344](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/PositionsManager.sol#L344)
```solidity
        if (_isLiquidatable(msg.sender, _poolToken, 0, _amount)) revert UnauthorisedBorrow();
```

[compound/PositionsManager.sol#L447](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/PositionsManager.sol#L447)
```solidity
        if (_isLiquidatable(_supplier, _poolToken, toWithdraw, 0)) revert UnauthorisedWithdraw();
```

[compound/PositionsManager.sol#L502-L505](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/PositionsManager.sol#L502-L505)
```solidity
        (vars.liquidationAllowed, vars.closeFactor) = _liquidationAllowed(
            _borrower,
            borrowPause.isDeprecated
        );
```
### Recommended Mitigation

Consider following the same approach implemented in the Morpho-Aave implementation inside `MorphoUtils._liquidityData` that will always be the token that the user has supplied/borrowed. 

Unlike Aave, Morpho's Compound implementation does not have a maximum hard-cap limit, which means that the `_isLiquidatable` loop could possibly revert because of an Out of Gas exception.

Ultimately, Morpho should always remember to always call updateP2PIndexes (for both Aave and Compound) before any logic inside the `*PositionsManager` (both Aave and Compound).
### Discussion
Morpho: Because liquidators are updating indexed to be able to perform liquidations and because it would drastically increase the gas cost, we decided not to implement this recommendation. 
Spearbit: Acknowledged.
### Notes & Impressions

#### Notes 
The Liquidation happens:
- The collateral value decreases
- The borrowed amount increases due to accumulated interest
The problem identified in this audit finding is that Morpho's implementation might make incorrect liquidation decisions because it doesn't always use up-to-date interest rate information for peer-to-peer (P2P) positions.

Here's a concrete example to illustrate: Alice makes three actions:

1. Supplies ETH to the pool
2. Supplies BAT in P2P mode
3. Borrows DAI against this collateral

Now imagine two market changes occur:

1. ETH price drops significantly
2. BAT interest rates increase substantially
#### Impressions

*This is a subtle issue requiring deep understanding of both the P2P lending mechanism and Morpho's index update model. *

### Tools
### Refine

- [[1-Business_Logic]]
---
## [M-12] Turning off an asset as collateral on Morpho-Aave still allows seizing of that  collateral on Morpho
----
- **Tags**: #business_logic 
- Number of finders: 5
- Difficulty: Hard
---
### Context
[aave-v2/MorphoGovernance.sol#L407-L413](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/aave-v2/MorphoGovernance.sol#L407-L413)
```solidity
    function setAssetAsCollateral(address _poolToken, bool _assetAsCollateral)
        external
        onlyOwner
        isMarketCreated(_poolToken)
    {
        pool.setUserUseReserveAsCollateral(market[_poolToken].underlyingToken, _assetAsCollateral);
    }
```

[aave-v2/MorphoUtils.sol#L285](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/aave-v2/MorphoUtils.sol#L285)
```solidity
                assetData.liquidationThreshold = 0;
```

### Description: 
The Morpho Aave deployment can set the asset to not be used as collateral for Aave's Morpho contract position. On Aave, this prevents liquidators from seizing this asset as collateral. 
1. However, this prevention does not extend to users on Morpho as Morpho has not implemented this check. Liquidations are performed through a `repay & withdraw` combination and withdrawing the asset on Aave is still allowed. 
2. When turning off the asset as collateral, the single Morpho contract position on Aave might still be overcollateralized, but some users on Morpho suddenly lose this asset as collateral (LTV becomes 0) and will be liquidated. 
### Recommendation: 
The feature does not work well with the current version of the Morpho Aave contracts. It must be enabled right from the beginning and may not be set later when users are already borrowing against the asset as collateral on Morpho. Clarify when this feature is supposed to be used, taking into consideration the mentioned issues. Reconsider if it's required.
### Discussion

### Notes & Impressions

#### Notes 
At the end, the Morpho team removed the `setAssetAsCollateral` function and code below
```solidity
// LTV and liquidation threshold should be zero if Morpho has not enabled this asset as collateral.
            if (
                !morphoPoolConfig.isUsingAsCollateral(pool.getReserveData(vars.underlyingToken).id)
            ) {
                assetData.ltv = 0;
                assetData.liquidationThreshold = 0;
            }
```
#### Impressions
*Think of it like this: Imagine you're building a car rental service on top of an existing car rental company. Initially, you might think you need your own system to mark cars as available or unavailable. But if you're always going to respect the underlying company's availability anyway, adding your own layer of availability controls just creates unnecessary complexity and potential for errors. It's better to simply pass through the underlying company's availability status.*

**Key Points**
- Position Aggregation: The aggregation contract combines all user positions into one large position on underlying protocols(Aave, compound).
	- The underlying protocols see one large pool of collateral owned by the aggregation contract
	- The aggregation contract internally tracks individual user positions
- Multi-Protocol Integration
	- interact
	- collateral 
	- liquidation
- Liquidation Mechanics
	- How: How can liquidation happen?
	- When: When can liquidation occur?
	- What: What prevents liquidation?
### Tools
### Refine

- [[1-Business_Logic]]

---
## [M-13] `claimToTreasury`(COMP) steals users' COMP rewards
----
- **Tags**: #Deposit_or_Reward_tokens #business_logic 
- Number of finders: 5
- Difficulty: Medium
---
### Description

The `claimToTreasury` function can send a market's underlying tokens that have been accumulated in the contract to the treasury. This is intended to be used for the reserve amounts that accumulate in the contract from P2P matches. However, Compound also pays out rewards in COMP and COMP is a valid Compound market. Sending the COMP reserves will also send the COMP rewards. This is especially bad as anyone can claim COMP rewards on the behalf of Morpho at any time and the rewards will be sent to the contract. An attacker could even front run a `claimToTreasury(cCOMP)` call with a `Comptroller.claimComp`(morpho, [cComp]) call to sabotage the reward system. Users won't be able to claim their rewards.

[compound/MorphoGovernance.sol#L399C1-L423C6](https://github.com/morpho-org/morpho-optimizers/blob/5f39e0d197830dbd4ede7f1657973a56f7e21232/contracts/compound/MorphoGovernance.sol#L399C1-L423C6)
```solidity
    function claimToTreasury(address[] calldata _poolTokens, uint256[] calldata _amounts)
        external
        onlyOwner
    {
        if (treasuryVault == address(0)) revert ZeroAddress();

        uint256 numberOfMarkets = _poolTokens.length;

        for (uint256 i; i < numberOfMarkets; ++i) {
            address poolToken = _poolTokens[i];

            Types.MarketStatus memory status = marketStatus[poolToken];
            if (!status.isCreated) continue;

            ERC20 underlyingToken = _getUnderlying(poolToken);
            uint256 underlyingBalance = underlyingToken.balanceOf(address(this)); //steals users rewards.

            if (underlyingBalance == 0) continue;

            uint256 toClaim = Math.min(_amounts[i], underlyingBalance);

            underlyingToken.safeTransfer(treasuryVault, toClaim);
            emit ReserveFeeClaimed(poolToken, toClaim);
        }
    }
```
### Recommended Mitigation

If Morpho wants to support the COMP market, consider separating the COMP reserve from the COMP rewards.
### Discussion

Morpho: Given the changes to do and the small likelihood to set a reserve factor for the COMP asset and the awareness on our side about this, we decided not to implement it. 

Spearbit: Acknowledged.
### Notes & Impressions

#### Notes 
This issue is indeed specifically related to unique characteristics of the COMP market, but in a somewhat indirect way. Let me explain why:

COMP has two special characteristics that make this situation unique:

1. COMP tokens are distributed as rewards to users of Compound protocol
2. COMP itself can be used as a market asset (cCOMP) within Compound

The core issue arises from the intersection of these characteristics with Morpho's `claimToTreasury` function. When this function is called for the COMP market, it can't distinguish between two different types of COMP tokens in the contract:

- COMP tokens that are legitimate reserves from P2P matches (which should go to treasury)
- COMP tokens that are rewards earned by users (which should go to the users)
#### Impressions

*This single balance check doesn't differentiate between reserves and rewards, which is problematic specifically because COMP serves both purposes simultaneously.*

### Tools
### Refine

- [[1-Business_Logic]]

---
---

## Audit Summary Notes
- {{summary_notes}}

## Tags
- Category: {{tags_category}}
- Priority:{{tags_priority}}