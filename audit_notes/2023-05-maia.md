
# 2023-05-maia
---
- Category: #Dexes #cross-chain #liquidity_manager #staking_pool 
- Note Create 2024-12-20
- Platform: code4rena
- Report Url: [2023-05-maia](https://code4rena.com/reports/2023-05-maia)
---
# High Risk Findings (xx)

---

{{Copy from High Risk Finding Template.md}}

---

# Medium Risk Findings (xx)

---
## [M-05] Replenishing gas is missing in `_payFallbackGas` of `RootBridgeAgent`

----
- **Tags**: #business_logic 
- Number of finders: 3
- Difficulty: Hard
---
The call `_payFallbackGas` is used to update the user deposit with the amount of gas needed to pay for the `fallback` function execution. However, it doesn't replenish gas. In other words, it doesn't deposit the `executionGasSpent` into `AnycallConfig` execution budget.
### Proof of Concept

Here is the method body:

[RootBridgeAgent.sol#L831-L846](https://github.com/code-423n4/2023-05-maia/blob/53b6231f0da68f91cf673ecb34867782e2f633e7/src/ulysses-omnichain/RootBridgeAgent.sol#L831-L846)
```solidity
	function _payFallbackGas(uint32 _settlementNonce, uint256 _initialGas) internal virtual {
		//Save gasleft
		uint256 gasLeft = gasleft();

		//Get Branch Environment Execution Cost
		uint256 minExecCost = tx.gasprice * (MIN_FALLBACK_RESERVE + _initialGas - gasLeft);

		//Check if sufficient balance
		if (minExecCost > getSettlement[_settlementNonce].gasToBridgeOut) {
			_forceRevert();
			return;
		}

		//Update user deposit reverts if not enough gas
		getSettlement[_settlementNonce].gasToBridgeOut -= minExecCost.toUint128();
	}
```

As you can see, there is no gas replenishing call.

`_payFallbackGas` is called at the end in `anyFallback` after reopening a user's settlement.

[RootBridgeAgent.sol#L1177](https://github.com/code-423n4/2023-05-maia/blob/53b6231f0da68f91cf673ecb34867782e2f633e7/src/ulysses-omnichain/RootBridgeAgent.sol#L1177)
```solidity
	function anyFallback(bytes calldata data)
		external
		virtual
		requiresExecutor
		returns (bool success, bytes memory result)
	{
		//Get Initial Gas Checkpoint
		uint256 _initialGas = gasleft();

		//Get fromChain
		(, uint256 _fromChainId) = _getContext();
		uint24 fromChainId = _fromChainId.toUint24();

		//Save Flag
		bytes1 flag = data[0];

		//Deposit nonce
		uint32 _settlementNonce;

		/// SETTLEMENT FLAG: 1 (single asset settlement)
		if (flag == 0x00) {
			_settlementNonce = uint32(bytes4(data[PARAMS_START_SIGNED:25]));
			_reopenSettlemment(_settlementNonce);

			/// SETTLEMENT FLAG: 1 (single asset settlement)
		} else if (flag == 0x01) {
			_settlementNonce = uint32(bytes4(data[PARAMS_START_SIGNED:25]));
			_reopenSettlemment(_settlementNonce);

			/// SETTLEMENT FLAG: 2 (multiple asset settlement)
		} else if (flag == 0x02) {
			_settlementNonce = uint32(bytes4(data[22:26]));
			_reopenSettlemment(_settlementNonce);
		}
		emit LogCalloutFail(flag, data, fromChainId);

		_payFallbackGas(_settlementNonce, _initialGas);

		return (true, "");
	}
```
### Recommended Mitigation Steps

Withdraw Gas from he port, unwrap it, then call `_replenishGas` to top up the execution budget.

### Discussion

### Notes & Impressions

#### Notes 
The recommended fix involves three steps:

1. Withdraw the required gas from the port
2. Unwrap the gas token if necessary
3. Call `_replenishGas` to properly fund the execution budget in AnycallConfig

A proper implementation might look something like this:
```solidity
function _payFallbackGas(uint32 _settlementNonce, uint256 _initialGas) internal virtual {
    // Existing gas calculation
    uint256 gasLeft = gasleft();
    uint256 minExecCost = tx.gasprice * (MIN_FALLBACK_RESERVE + _initialGas - gasLeft);
    
    // Verify sufficient balance
    if (minExecCost > getSettlement[_settlementNonce].gasToBridgeOut) {
        _forceRevert();
        return;
    }
    
    // Update user deposit
    getSettlement[_settlementNonce].gasToBridgeOut -= minExecCost.toUint128();
    
    // Added steps:
    // 1. Withdraw gas from port
    port.withdrawGas(minExecCost);
    
    // 2. Unwrap gas token if needed
    // (implementation depends on specific token handling)
    
    // 3. Replenish execution budget
    anycallConfig.depositGas{value: minExecCost}();
}
```

#### Impressions

To better understand why this is problematic, consider this analogy: Imagine you're using a prepaid phone service that requires you to transfer credit before making international calls. If the system only deducts credit from your account but doesn't actually transfer it to the international carrier, your calls would fail even though you've "paid" for them.

### Tools
### Refine
- [[1-Business_Logic]]

---
## [M-31] Incorrect accounting logic for `fallback` gas will lead to insolvency

----
- **Tags**: #business_logic 
- Number of finders: 1
- Difficulty: Hard
---
### Lines of code

[RootBridgeAgent.sol#L823](https://github.com/code-423n4/2023-05-maia/blob/54a45beb1428d85999da3f721f923cbf36ee3d35/src/ulysses-omnichain/RootBridgeAgent.sol#L823)
```solidity
    function _payExecutionGas(uint128 _depositedGas, uint128 _gasToBridgeOut, uint256 _initialGas, uint24 _fromChain)
        internal
    {
        //reset initial remote execution gas and remote execution fee information
        delete(initialGas);
        delete(userFeeInfo);


        if (_fromChain == localChainId) return;


        //Get Available Gas
        uint256 availableGas = _depositedGas - _gasToBridgeOut;


        //Get Root Environment Execution Cost
        uint256 minExecCost = tx.gasprice * (MIN_EXECUTION_OVERHEAD + _initialGas - gasleft());


        //Check if sufficient balance
        if (minExecCost > availableGas) {
            _forceRevert();
            return;
        }


        //Replenish Gas
        _replenishGas(minExecCost);


        //Account for excess gas
        accumulatedFees += availableGas - minExecCost;
    }
```

[BranchBridgeAgent.sol#L1044](https://github.com/code-423n4/2023-05-maia/blob/54a45beb1428d85999da3f721f923cbf36ee3d35/src/ulysses-omnichain/BranchBridgeAgent.sol#L1044)
```solidity
    function _payExecutionGas(address _recipient, uint256 _initialGas) internal virtual {
        //Gas remaining
        uint256 gasRemaining = wrappedNativeToken.balanceOf(address(this));


        //Unwrap Gas
        wrappedNativeToken.withdraw(gasRemaining);


        //Delete Remote Initiated Action State
        delete(remoteCallDepositedGas);


        ///Save gas left
        uint256 gasLeft = gasleft();


        //Get Branch Environment Execution Cost
        uint256 minExecCost = tx.gasprice * (MIN_EXECUTION_OVERHEAD + _initialGas - gasLeft);


        //Check if sufficient balance
        if (minExecCost > gasRemaining) {
            _forceRevert();
            return;
        }


        //Replenish Gas
        _replenishGas(minExecCost);


        //Transfer gas remaining to recipient
        SafeTransferLib.safeTransferETH(_recipient, gasRemaining - minExecCost);


        //Save Gas
        uint256 gasAfterTransfer = gasleft();


        //Check if sufficient balance
        if (gasLeft - gasAfterTransfer > TRANSFER_OVERHEAD) {
            _forceRevert();
            return;
        }
    }
```

### Proof of Concept

```solidity
// on root chain
function _payExecutionGas(uint128 _depositedGas, uint128 _gasToBridgeOut, uint256 _initialGas, uint24 _fromChain) internal {
    ......
    uint256 availableGas = _depositedGas - _gasToBridgeOut;
    uint256 minExecCost = tx.gasprice * (MIN_EXECUTION_OVERHEAD + _initialGas - gasleft());

    if (minExecCost > availableGas) {
        _forceRevert();
        return;
    }

    _replenishGas(minExecCost);

    //Account for excess gas
    accumulatedFees += availableGas - minExecCost;
}

// on branch chain
function _payFallbackGas(uint32 _depositNonce, uint256 _initialGas) internal virtual {
    ......
    IPort(localPortAddress).withdraw(address(this), address(wrappedNativeToken), minExecCost);
    wrappedNativeToken.withdraw(minExecCost);
    _replenishGas(minExecCost);
}
```

As above, when paying execution gas on the root chain, the excessive gas is added to `accumulatedFees`. So theoretically, all deposited gas is used up and no gas has been reserved for `anyFallback` on the branch chain. The `withdrawl` in `_payFallbackGas` on the branch chain will cause insolvency:

```solidity
// on branch chain
function _payExecutionGas(address _recipient, uint256 _initialGas) internal virtual {
    ......
    uint256 gasLeft = gasleft();
    uint256 minExecCost = tx.gasprice * (MIN_EXECUTION_OVERHEAD + _initialGas - gasLeft);

    if (minExecCost > gasRemaining) {
        _forceRevert();
        return;
    }

    _replenishGas(minExecCost);

    //Transfer gas remaining to recipient
    SafeTransferLib.safeTransferETH(_recipient, gasRemaining - minExecCost);
    ......
    }
}

// on root chain
function _payFallbackGas(uint32 _settlementNonce, uint256 _initialGas) internal virtual {
    uint256 gasLeft = gasleft();
    uint256 minExecCost = tx.gasprice * (MIN_FALLBACK_RESERVE + _initialGas - gasLeft);

    if (minExecCost > getSettlement[_settlementNonce].gasToBridgeOut) {
        _forceRevert();
        return;
    }

    getSettlement[_settlementNonce].gasToBridgeOut -= minExecCost.toUint128();
}
```

As above, when paying execution gas on the branch chain, the excessive gas has be sent to the recipent. So therotically, all deposited gas is used up and no gas has been reserved for `anyFallback` on the root chain. `_payFallbackGas` does not `_replenishGas`, which will cause insolvency of the gas budget in `AnycallConfig`.
### Recommended Mitigation

Deduct `fallback` gas from deposited gas.
### Discussion

**[xuwinnie (warden) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1655754684):**

> Hey, I believe this is not a dup of [#786](https://github.com/code-423n4/2023-05-maia-findings/issues/786). This issue is mainly about accounting logic. I have described two scenes:
> 
> 1. Execute on `root` and `fallback` on branch: insolvency of the port's weth balance.
>     
> 2. Execute on `branch` and `fallback` on root: insolvency of the budget.
>     
> 
> Even though fix from #786 is applied, the accounting logic is still incorrect. If the port's balance is reduced, it comes to scene 1: insolvency of the port's balance.
> 
> And this issue will cause insolvency of h-weth, so I think it reaches high.

**[0xBugsy (Maia) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1655877017):**

> > As above, when paying execution gas on the root chain, the excessive gas is added to `accumulatedFees`. So theoretically, all deposited gas is used up and no gas has been reserved for `anyFallback` on the branch chain. The withdrawal in `_payFallbackGas` on the branch chain will cause insolvency.
> 
> 1. This isn't accurate. `fallback` gas for a call from the Root -> Branch is enforced and allocated in `_manageGasOut`, not `_payExecutionGas`, so the proposed fix will not lead to `hToken` insolvency on the Root. Although, the proposed fix should have the added detail that the balance should be obtained from `bridgeToRoot` and not a withdrawal. This can only be done once per failed deposit state, meaning it would need to be set to true and `FALLBACK_RESERVE` replenished to be deducted again.
>     
> 
> > As above, when paying execution gas on the branch chain, the excessive gas has be sent to the recipent. So theoretically, all deposited gas is used up and no gas has been reserved for `anyFallback` on the root chain. `_payFallbackGas` does not `_replenishGas`, which will cause insolvency of the gas budget in `AnycallConfig`.
> 
> 1. This is also invalid since `MIN_FALLBACK_RESERVE` is enforced for keeping deposited gas in the Branch Port and gas is replenished upon `_payFallbackGas` withdrawing from the Port in an appropriate manner.
>     
> 
> I believe this was marked as a duplicate, owing to the fact that in 1. you described a situation in #786, where a error exists and proposed the same appropriate fix.

**[xuwinnie (warden) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656470345):**

> Thanks for explaining @0xBugsy. To make my point clearer, I'll give an example:
> 
> Suppose a user calls `retrieveDeposit` and deposited 20 unit gas. `depositedGas` is 20 and `gasToBridgeOut(remoteExecutionGas)` is `0`. On the root chain, the whole process does not involve `_manageGasOut`. In `_payExecutionGas`, suppose 12 unit is replenished and then 8 unit is added to `accumulatedFees`. On the branch chain, `fallback` costs 14 gas, and then 14 units are withdrawn from the port and replenished. Overall: 20 units in, 34 units out.
> 
> As you mentioned, I believe `_manageGasOut` should be used to manage `fallback` gas, but it seems to be only managing remote execution gas. I'm not sure I've understood everything correctly, if I misunderstood something, please tell me.

**[0xBugsy (Maia) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656676212):**

> I believe you are not considering the fact that `Fallback` Gas is reserved every time a remote call is initiated. So if in your scenario you are calling `retrieveDeposit`, this means that the deposit already has `fallback` gas reserved in the origin branch. We are also sure that `fallback` is yet to be triggered, so this balance has not been double spent. This is enforced directly in the callout functions in branches, whereas in the Root, this is enforced in the `_manageGasOut` where gas minimum is checked and assets are converted to destination chain gas.
> 
> Hope this made it clearer!

**[0xBugsy (Maia) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656676910):**

> We recognize the audit's findings on Anycall Gas Management. These will not be rectified due to the upcoming migration of this section to LayerZero.

**[xuwinnie (warden) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656714298):**

> Hey @Trust @0xBugsy - sorry for delaying the judging process but I still need to add something.
> 
> > "that deposit already has `fallback` gas reserved in the origin branch. We are also sure that `fallback` is yet to be triggered, so this balance has not been double spent."  
> > This is not true. The balance is double spent. Let's suppose the user deposited this gas in a tx on the branch. On the root chain, although tx fails and `anyExecute` returns false, gas is still charged (since it is not `forceReverted`). So double spending occurs (on root `anyExecute` and branch `anyFallback`).

**[0xBugsy (Maia) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656723672):**

> I believe there may have been some language barrier in our communication but what I now believe has happened is:
> 
> 1. You disclosed everything that was covered in detail in #786.
>     
> 2. Added the fact, that opposed to what #786 claims, porting the Branch functioning is not enough since once initiating a cross-chain call. We should always deduct the chain's `FALLBACK_RESERVE` from the deposited gas (in the root deduct branch `fallback` reserve gas units and in branch `reverse`), which would mean the solution put forward in #786 is not 100% accurate complete .
>     
> 
> By the way, this was not at all made obvious in the issue took some reading between the lines, but happy we got to some understanding. Obviously, do correct me if my interpretation of what was said is incorrect in any way.

**[xuwinnie (warden) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656726800):**

> @0xBugsy - Yeah, this is what I want to say. I'm sorry if my previous expression is not clear enough!

**[xuwinnie (warden) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656729258):**

> Hi @Trust - to conclude, the core issue I described here is double spending of deposited gas which will lead to insolvency of the port's weth. I believe none of 786 or its dups has mentioned it. Thanks for your attention!

**[Trust (judge) commented](https://github.com/code-423n4/2023-05-maia-findings/issues/313#issuecomment-1656744312):**

> Upon further inspection, the warden has uncovered a different root cause than previously dupped submissions. The risks associated are deemed of Medium severity.
### Notes & Impressions

#### Notes 
- Reserve fallback gas upfront when starting cross-chain operations
- Only treat truly excess gas as available for fees
- Track and enforce these reservations on both chains

#### Impressions
- *The discussion of this findings is more valuable*

### Tools
### Refine
- [[1-Business_Logic]]

---

---

## Audit Summary Notes
- {{summary_notes}}

## Tags
- Category: {{tags_category}}
- Priority:{{tags_priority}}